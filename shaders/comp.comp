#version 450
layout (local_size_x = 256) in;
precision highp float;

#define PI 3.1415926538

#define list_size 12

struct Particle {
    vec3 x;                     // position
    float h;                    // radius
    vec3 v;                     // velocity
    float m;                    // mass

    float rho;                  // density
    float p;                    // pressure

    float staticP;               // is particle static
    float padding;
};

layout(set = 0, binding = 0) uniform UBO {
    float desired_density;
    float k;                    // normalization constant / stiffness constant
    float nr_of_particles;
    float kernelh;

    vec4 gridA;
    vec4 gridB;
} ubo;

layout(set = 0, binding = 1, std430)  buffer inBuffer {
    Particle p[];
} in_data;

layout(set = 0, binding = 2, std430) buffer outBuffer {
    Particle p[];
} out_data;

layout(set = 0, binding = 3, std430) buffer gridBuffer {
    int numbers[];
} grid_data;

//----------------------------------------------------------------------------------------------------------------------
struct Neighbourhood {
    vec3 neighbour[27]; // 3^3
};

int cellx = int(ceil(abs((ubo.gridB.x - ubo.gridA.x)/ubo.kernelh))); // Number of cells in x direction
int celly = int(ceil(abs((ubo.gridB.y - ubo.gridA.y)/ubo.kernelh))); // Number of cells in y direction
int cellz = int(ceil(abs((ubo.gridB.z - ubo.gridA.z)/ubo.kernelh))); // Number of cells in z direction

vec3 determineGridTuple(int particleIdx) {
    vec3 diff = ubo.gridA.xyz - in_data.p[particleIdx].x;
    diff /= ubo.kernelh;

    return floor(abs(diff));
}

int getStartIdxOfCell(vec3 tuple) {
    return int((tuple.x * cellz * celly + tuple.y * cellz + tuple.z)*list_size);
}

Neighbourhood getNeighbouringCells(vec3 cellTuple) {
    Neighbourhood ret;

    for (int i = 0; i < 3; i++) {
        for (int j = 0; j < 3; j++) {
            for (int k = 0; k < 3; k++) {
                ret.neighbour[i * 9 + j * 3 + k] = vec3(cellTuple.x + (i-1), cellTuple.y + (j-1), cellTuple.z + (k-1));
            }
        }
    }

    return ret;
}

bool checkBounds(vec3 tuple) {
    if (tuple.x < 0 || tuple.x >= cellx)
        return false;

    if (tuple.y < 0 || tuple.y >= celly)
        return false;

    if (tuple.z < 0 || tuple.z >= cellz)
        return false;

    return true;
}
//----------------------------------------------------------------------------------------------------------------------

float m_k = 8.0 / (PI * pow(ubo.kernelh, 3));
float m_l = 1.0/(pow(ubo.kernelh, 3)) * 3.0/(2.0*PI);

float kernel(vec3 i, vec3 j) {
    float q = length(i - j) / ubo.kernelh;


    float ret = 0;
    if (q <= 1.0) {
        if (q <= 0.5) {
            ret = m_k * (6.0 * (pow(q, 3.0) - pow(q, 2.0)) + 1.0);
        } else {
            ret = m_k * (2.0 * pow(1 - q, 3));
        }
    }

    return ret;
}

void copyData(uint gID) {
    out_data.p[gID].x = in_data.p[gID].x;
    out_data.p[gID].h = in_data.p[gID].h;
    out_data.p[gID].v = in_data.p[gID].v;
    out_data.p[gID].m = in_data.p[gID].m;

    out_data.p[gID].rho = in_data.p[gID].rho;
    out_data.p[gID].p = in_data.p[gID].p;

    out_data.p[gID].staticP = in_data.p[gID].staticP;
    out_data.p[gID].padding = 1;
}

void calculateRhoAndP(uint gID) {
    float density = 0.0;

    /*for (int j = 0; j < ubo.nr_of_particles; j++) {
        if (gID == j)
            continue;

        density += in_data.p[gID].m * kernel(in_data.p[gID].x, in_data.p[j].x);
    }*/

    vec3 tuple = determineGridTuple(int(gID));
    Neighbourhood n = getNeighbouringCells(tuple);
    for (int i = 0; i < 27; i++) {
        vec3 current = n.neighbour[i];
        if (!checkBounds(current)) continue;

        int size = grid_data.numbers[getStartIdxOfCell(current)];
        int iterIdx = 0;
        for (int j = 1; j < size; j++) {
            iterIdx = grid_data.numbers[j];
            if (gID == iterIdx) continue;

            density += in_data.p[gID].m * kernel(in_data.p[gID].x, in_data.p[iterIdx].x);
        }
    }

    out_data.p[gID].rho = density;

    float ratio = density / ubo.desired_density;
    float p = 0.001 * (pow(ratio, 7) - 1.0);
    out_data.p[gID].p = p < 0 ? 0 : p;
}

void main()
{
    //grab global ID
    uint gID = gl_GlobalInvocationID.x;

    if (gID < ubo.nr_of_particles) {
        copyData(gID);
        calculateRhoAndP(gID);
    }
}